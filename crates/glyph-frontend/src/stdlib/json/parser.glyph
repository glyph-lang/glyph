// Minimal JSON Parser - Uses only basic Glyph syntax

from std/json import JsonValue, ParseError, ParseResult
from std/enums import Option
from std/map import Map
from std/string import byte_at, atof, string_from_byte
from std/vec import Vec

struct Parser {
  input: str,
  pos: usize,
  len: usize,
}

struct ParsedHex {
  value: i32,
  ok: i32,
}

fn make_parse_error(msg: String, pos: usize) -> ParseError {
  ret ParseError { message: msg, position: pos }
}

fn skip_ws(p: &mut Parser) -> usize {
  let mut pos = p.pos
  let len = p.len
  let input = p.input
  while pos < len {
    let ch = byte_at(input, pos)
    // space, tab, CR, LF
    if ch == 32 || ch == 9 || ch == 13 || ch == 10 {
      pos = pos + 1
    } else {
      p.pos = pos
      ret pos
    }
  }
  p.pos = pos
  ret pos
}

fn is_digit(ch: u8) -> bool {
  ret ch >= 48 && ch <= 57
}

fn hex_value(ch: u8) -> i32 {
  if ch >= 48 && ch <= 57 {
    ret ch - 48
  }
  if ch >= 65 && ch <= 70 {
    ret ch - 55
  }
  if ch >= 97 && ch <= 102 {
    ret ch - 87
  }
  ret 0 - 1
}

fn parse_hex4(input: str, pos: usize) -> ParsedHex {
  let len = input.len()
  if pos + 3 >= len {
    ret ParsedHex { value: 0, ok: 0 }
  }
  let d0 = hex_value(byte_at(input, pos))
  if d0 < 0 {
    ret ParsedHex { value: 0, ok: 0 }
  }
  let d1 = hex_value(byte_at(input, pos + 1))
  if d1 < 0 {
    ret ParsedHex { value: 0, ok: 0 }
  }
  let d2 = hex_value(byte_at(input, pos + 2))
  if d2 < 0 {
    ret ParsedHex { value: 0, ok: 0 }
  }
  let d3 = hex_value(byte_at(input, pos + 3))
  if d3 < 0 {
    ret ParsedHex { value: 0, ok: 0 }
  }
  let code = d0 * 4096 + d1 * 256 + d2 * 16 + d3
  ret ParsedHex { value: code, ok: 1 }
}

fn rem_64(value: i32) -> i32 {
  ret value - (value / 64) * 64
}

fn push_utf8_codepoint(segments0: Vec<String>, code: i32) -> Vec<String> {
  let mut segments = segments0
  if code <= 127 {
    segments.push(string_from_byte(code))
    ret segments
  }
  if code <= 2047 {
    let hi = code / 64
    let lo = rem_64(code)
    let b1 = 192 + hi
    let b2 = 128 + lo
    segments.push(string_from_byte(b1))
    segments.push(string_from_byte(b2))
    ret segments
  }
  if code <= 65535 {
    let mid = code / 64
    let b1 = 224 + (code / 4096)
    let b2 = 128 + rem_64(mid)
    let b3 = 128 + rem_64(code)
    segments.push(string_from_byte(b1))
    segments.push(string_from_byte(b2))
    segments.push(string_from_byte(b3))
    ret segments
  }
  if code <= 1114111 {
    let mid1 = code / 4096
    let mid2 = code / 64
    let b1 = 240 + (code / 262144)
    let b2 = 128 + rem_64(mid1)
    let b3 = 128 + rem_64(mid2)
    let b4 = 128 + rem_64(code)
    segments.push(string_from_byte(b1))
    segments.push(string_from_byte(b2))
    segments.push(string_from_byte(b3))
    segments.push(string_from_byte(b4))
    ret segments
  }
  ret segments
}

fn parse_literal(input: str, pos: usize, lit: str) -> bool {
  let lit_len = lit.len()
  let in_len = input.len()
  if pos + lit_len > in_len {
    ret false
  }

  let i0 = 0
  ret parse_literal_loop(input, pos, lit, lit_len, i0)
}

fn parse_literal_loop(input: str, pos: usize, lit: str, lit_len: usize, i: usize) -> bool {
  if i >= lit_len {
    ret true
  }
  if byte_at(input, pos + i) != byte_at(lit, i) {
    ret false
  }
  ret parse_literal_loop(input, pos, lit, lit_len, i + 1)
}

fn parse_number(p: &mut Parser) -> ParseResult<JsonValue> {
  let input = p.input
  let len = p.len
  let mut i = p.pos
  if i < len && byte_at(input, i) == 45 {
    i = i + 1
  }

  if i >= len {
    ret Err(ParseError { message: String::from_str("expected digit"), position: i })
  }
  if is_digit(byte_at(input, i)) {
    // ok
  } else {
    ret Err(ParseError { message: String::from_str("expected digit"), position: i })
  }

  i = parse_digits(input, len, i)

  // fraction
  if i < len && byte_at(input, i) == 46 {
    i = i + 1
    if i >= len {
      ret Err(ParseError { message: String::from_str("expected digit after '.'"), position: i })
    }
    if is_digit(byte_at(input, i)) {
      // ok
    } else {
      ret Err(ParseError { message: String::from_str("expected digit after '.'"), position: i })
    }
    i = parse_digits(input, len, i)
  }

  // exponent (subset)
  if i < len {
    let e = byte_at(input, i)
    if e == 101 || e == 69 {
      i = i + 1
      if i < len {
        let s = byte_at(input, i)
        if s == 43 || s == 45 {
          i = i + 1
        }
      }
      if i >= len {
        ret Err(ParseError { message: String::from_str("expected digit in exponent"), position: i })
      }
      if is_digit(byte_at(input, i)) {
        // ok
      } else {
        ret Err(ParseError { message: String::from_str("expected digit in exponent"), position: i })
      }
      i = parse_digits(input, len, i)
    }
  }

  let slice = input.slice(p.pos, i - p.pos)
  let n = atof(slice)
  p.pos = i
  ret Ok(JsonValue::Number(n))
}

fn parse_digits(input: str, len: usize, i0: usize) -> usize {
  if i0 >= len {
    ret i0
  }
  if is_digit(byte_at(input, i0)) {
    // ok
  } else {
    ret i0
  }
  ret parse_digits(input, len, i0 + 1)
}

fn parse_string(p: &mut Parser) -> ParseResult<String> {
  let input = p.input
  let len = p.len
  let pos = p.pos
  if pos >= len || byte_at(input, pos) != 34 {
    ret Err(make_parse_error(String::from_str("expected string"), pos))
  }
  let mut i = pos + 1
  let mut seg_start = i
  let mut had_escape = false
  let mut segments: Vec<String> = Vec::new()

  while i < len {
    let ch = byte_at(input, i)
    if ch == 34 {
      if had_escape == false {
        let s = input.slice(pos + 1, i - (pos + 1))
        p.pos = i + 1
        ret Ok(s)
      }
      if seg_start < i {
        let tail = input.slice(seg_start, i - seg_start)
        segments.push(tail)
      }
      let s = concat_segments(segments)
      p.pos = i + 1
      ret Ok(s)
    }

    if ch < 32 {
      ret Err(make_parse_error(String::from_str("unescaped control character"), i))
    }

    if ch == 92 {
      had_escape = true
      if seg_start < i {
        let part = input.slice(seg_start, i - seg_start)
        segments.push(part)
      }

      if i + 1 >= len {
        ret Err(make_parse_error(String::from_str("unterminated escape"), i))
      }

      let next = byte_at(input, i + 1)
      if next == 34 {
        segments.push(string_from_byte(34))
        i = i + 2
        seg_start = i
        cont
      }
      if next == 92 {
        segments.push(string_from_byte(92))
        i = i + 2
        seg_start = i
        cont
      }
      if next == 47 {
        segments.push(string_from_byte(47))
        i = i + 2
        seg_start = i
        cont
      }
      if next == 98 {
        segments.push(string_from_byte(8))
        i = i + 2
        seg_start = i
        cont
      }
      if next == 102 {
        segments.push(string_from_byte(12))
        i = i + 2
        seg_start = i
        cont
      }
      if next == 110 {
        segments.push(string_from_byte(10))
        i = i + 2
        seg_start = i
        cont
      }
      if next == 114 {
        segments.push(string_from_byte(13))
        i = i + 2
        seg_start = i
        cont
      }
      if next == 116 {
        segments.push(string_from_byte(9))
        i = i + 2
        seg_start = i
        cont
      }

      if next == 117 {
        let esc_pos = i
        let hex = parse_hex4(input, i + 2)
        if hex.ok == 0 {
          ret Err(make_parse_error(String::from_str("invalid unicode escape"), esc_pos))
        }
        let code: i32 = hex.value

          if code == 0 {
            ret Err(make_parse_error(String::from_str("nul escape unsupported"), esc_pos))
          }

        if code >= 55296 && code <= 56319 {
          if i + 11 >= len {
            ret Err(make_parse_error(String::from_str("invalid unicode surrogate"), esc_pos))
          }
          if byte_at(input, i + 6) != 92 || byte_at(input, i + 7) != 117 {
            ret Err(make_parse_error(String::from_str("invalid unicode surrogate"), esc_pos))
          }

          let low_hex = parse_hex4(input, i + 8)
          if low_hex.ok == 0 {
            ret Err(make_parse_error(String::from_str("invalid unicode escape"), esc_pos))
          }
          let low: i32 = low_hex.value
          if low < 56320 || low > 57343 {
            ret Err(make_parse_error(String::from_str("invalid unicode surrogate"), esc_pos))
          }

          let high: i32 = code - 55296
          let low_adj: i32 = low - 56320
          let codepoint: i32 = 65536 + (high * 1024) + low_adj
          segments = push_utf8_codepoint(segments, codepoint)
          i = i + 12
          seg_start = i
          cont
        }

        if code >= 56320 && code <= 57343 {
          ret Err(make_parse_error(String::from_str("invalid unicode surrogate"), esc_pos))
        }

        segments = push_utf8_codepoint(segments, code)
        i = i + 6
        seg_start = i
        cont
      }

      ret Err(make_parse_error(String::from_str("invalid escape"), i))
    }

    i = i + 1
  }

  ret Err(make_parse_error(String::from_str("unterminated string"), pos))
}

fn concat_segments(segments0: Vec<String>) -> String {
  let mut segments = segments0
  if segments.len() == 0 {
    ret String::from_str("")
  }

  while segments.len() > 1 {
    let mut next: Vec<String> = Vec::new()

    while segments.len() > 1 {
      let right = segments.pop()
      let left = segments.pop()
      let merged = match left {
        Some(l) => match right {
          Some(r) => l.concat(r),
          None => l,
        },
        None => String::from_str(""),
      }
      next.push(merged)
    }

    let leftover = segments.pop()
    match leftover {
      Some(s) => next.push(s),
      None => {},
    }

    segments = reverse_segments(next)
  }

  let out = segments.pop()
  ret match out {
    Some(s) => s,
    None => String::from_str(""),
  }
}

fn reverse_segments(input: Vec<String>) -> Vec<String> {
  let mut src = input
  let mut out: Vec<String> = Vec::new()
  while src.len() > 0 {
    let v = src.pop()
    match v {
      Some(s) => out.push(s),
      None => {},
    }
  }
  ret out
}

fn parse_array(p: &mut Parser) -> ParseResult<JsonValue> {
  let input = p.input
  let len = p.len
  let pos = p.pos
  if pos >= len || byte_at(input, pos) != 91 {
    ret Err(ParseError { message: String::from_str("expected '['"), position: pos })
  }

  p.pos = pos + 1
  let mut i = skip_ws(p)
  let mut arr: Vec<JsonValue> = Vec::new()

  if i < len && byte_at(input, i) == 93 {
    p.pos = i + 1
    ret Ok(JsonValue::Array(arr))
  }

  ret parse_array_elems(p, arr)
}

fn parse_array_elems(p: &mut Parser, arr0: Vec<JsonValue>) -> ParseResult<JsonValue> {
  let pv = parse_value(p)
  ret match pv {
    Ok(v) => parse_array_after_value(p, v, arr0),
    Err(e) => Err(e),
  }
}

fn parse_array_after_value(p: &mut Parser, value: JsonValue, arr0: Vec<JsonValue>) -> ParseResult<JsonValue> {
  let input = p.input
  let len = p.len
  let mut arr = arr0
  arr.push(value)
  let mut i = skip_ws(p)

  if i >= len {
    ret Err(ParseError { message: String::from_str("unterminated array"), position: i })
  }

  let ch = byte_at(input, i)
  if ch == 44 {
    p.pos = i + 1
    i = skip_ws(p)
    ret parse_array_elems(p, arr)
  }

  if ch == 93 {
    p.pos = i + 1
    ret Ok(JsonValue::Array(arr))
  }

  ret Err(ParseError { message: String::from_str("expected ',' or ']'"), position: i })
}

fn parse_object(p: &mut Parser) -> ParseResult<JsonValue> {
  let input = p.input
  let len = p.len
  let pos = p.pos
  if pos >= len || byte_at(input, pos) != 123 {
    ret Err(ParseError { message: String::from_str("expected '{'"), position: pos })
  }

  p.pos = pos + 1
  let mut i = skip_ws(p)
  let mut obj: Map<String, JsonValue> = Map::new()

  if i < len && byte_at(input, i) == 125 {
    p.pos = i + 1
    ret Ok(JsonValue::Object(obj))
  }

  ret parse_object_pairs(p, obj)
}

fn parse_object_pairs(p: &mut Parser, obj0: Map<String, JsonValue>) -> ParseResult<JsonValue> {
  let pk = parse_string(p)
  ret match pk {
    Ok(k) => parse_object_after_key(p, k, obj0),
    Err(e) => Err(e),
  }
}

fn parse_object_after_key(
  p: &mut Parser,
  k: String,
  obj0: Map<String, JsonValue>,
) -> ParseResult<JsonValue> {
  let input = p.input
  let len = p.len
  let mut i = skip_ws(p)

  if i >= len || byte_at(input, i) != 58 {
    ret Err(ParseError { message: String::from_str("expected ':'"), position: i })
  }
  p.pos = i + 1
  i = skip_ws(p)

  let pv = parse_value(p)
  ret match pv {
    Ok(v) => parse_object_after_value(p, k, v, obj0),
    Err(e) => Err(e),
  }
}

fn parse_object_after_value(
  p: &mut Parser,
  k: String,
  value: JsonValue,
  obj0: Map<String, JsonValue>,
) -> ParseResult<JsonValue> {
  let input = p.input
  let len = p.len
  let mut obj = obj0
  let _ = obj.add(k, value)

  let mut i = skip_ws(p)
  if i >= len {
    ret Err(ParseError { message: String::from_str("unterminated object"), position: i })
  }

  let ch = byte_at(input, i)
  if ch == 44 {
    p.pos = i + 1
    i = skip_ws(p)
    ret parse_object_pairs(p, obj)
  }

  if ch == 125 {
    p.pos = i + 1
    ret Ok(JsonValue::Object(obj))
  }

  ret Err(ParseError { message: String::from_str("expected ',' or '}'"), position: i })
}

fn parse_value(p: &mut Parser) -> ParseResult<JsonValue> {
  let pos = skip_ws(p)
  let input = p.input
  let len = p.len
  if pos >= len {
    ret Err(ParseError { message: String::from_str("unexpected end of input"), position: pos })
  }

  if parse_literal(input, pos, "null") {
    p.pos = pos + 4
    ret Ok(JsonValue::Null())
  }
  if parse_literal(input, pos, "true") {
    p.pos = pos + 4
    ret Ok(JsonValue::Bool(true))
  }
  if parse_literal(input, pos, "false") {
    p.pos = pos + 5
    ret Ok(JsonValue::Bool(false))
  }

  let ch = byte_at(input, pos)
  if ch == 34 {
    let ps = parse_string(p)
    ret match ps {
      Ok(s) => Ok(JsonValue::String(s)),
      Err(e) => Err(e),
    }
  }

  if ch == 91 {
    ret parse_array(p)
  }

  if ch == 123 {
    ret parse_object(p)
  }

  if ch == 45 || is_digit(ch) {
    ret parse_number(p)
  }

  ret Err(ParseError { message: String::from_str("unexpected token"), position: pos })
}

fn parse(input: str) -> ParseResult<JsonValue> {
  let mut p = Parser { input: input, pos: 0, len: input.len() }
  let pv = parse_value(&mut p)
  ret match pv {
    Ok(v) => parse_finish(&mut p, v),
    Err(e) => Err(e),
  }
}

fn parse_finish(p: &mut Parser, value: JsonValue) -> ParseResult<JsonValue> {
  let end = skip_ws(p)
  if end != p.len {
    ret Err(ParseError { message: String::from_str("trailing characters"), position: end })
  }
  ret Ok(value)
}
